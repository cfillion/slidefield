background = {
  \image background_src {
    size = LAYOUT_SIZE
  }

  \text {
    content = "<b>Photo Credit:</b> "
    content += background_link

    position = 10x-30
    position += (y)(y)LAYOUT_SIZE

    color = #FFFFFFFF
    z_order = 1

    \text {
      position += 1x1
      color = #000000FF
      z_order -= 1
    }
  }
}

title_tpl = {
  color = #FFFFFFFF
  font = TITLE_FONT
  height = 100
  position = 100x70
  z_order = 1

  % support alignment
  x = (x)position
  x *= 2
  width = (x)LAYOUT_SIZE
  width -= x

  % shadow
  \text {
    color = #00000099
    position += 2x2
    z_order -= 1
  }
}

infobox = {
  \rect {
    padding = 10
    text_height = 40
    z_order = 1

    box_height = (lines)infos
    box_height *= text_height
    box_height += padding

    size = 650x0
    size += (y)box_height

    position = LAYOUT_SIZE
    position -= size
    position /= 2x1
    position -= 0x100

    fill = #FFFFFF69

    \text infos {
      position += (x)padding
      position += (y)padding

      height = text_height
      color = #011010FF
      font = INFO_FONT

      % shadow
      \text {
        position += 1x1
        color = #FFFFFF99
        z_order -= 1
      }
    }
  }
}

demo_template = {
  \&background

  \text {
    content = "<b>"
    content += title

    \&title_tpl
  }

%  \animation "slide left" {
  \&infobox
}
